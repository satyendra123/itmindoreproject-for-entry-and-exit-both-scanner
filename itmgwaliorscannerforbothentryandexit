#include <SPI.h>
#include <Ethernet.h>

#define RELAY 9
#define EXIT_RELAY 10
#define QR_BAUD 115200

#define QR_BUF 128
#define TCP_BUF 128

EthernetServer server(6000);
EthernetClient client;

// ------------ ENTRY QR VARIABLES (Serial1) ------------
char enBuffer[QR_BUF];
uint8_t enIndex = 0;
unsigned long enLastByteTime = 0;

// ------------ EXIT QR VARIABLES (Serial2) ------------
char exBuffer[QR_BUF];
uint8_t exIndex = 0;
unsigned long exLastByteTime = 0;

const unsigned long qrTimeout = 25;

// ------------ TCP VARIABLES ------------
char tcpBuffer[TCP_BUF];
uint8_t tcpIndex = 0;

unsigned long lastHeartbeat = 0;
const unsigned long heartbeatInterval = 3000;

// ------------ NETWORK CONFIG ------------
byte mac[] = { 0xCD, 0xAD, 0xBE, 0xEF, 0xFE, 0x03 };
IPAddress ip(192, 168, 60, 15);

// ------------ UTILITY FUNCTIONS ------------
void trimString(char *str) {
  while (*str == '\r' || *str == '\n' || *str == ' ')
    memmove(str, str + 1, strlen(str));
  int len = strlen(str);
  while (len > 0 && (str[len - 1] == '\r' || str[len - 1] == '\n' || str[len - 1] == ' '))
    str[--len] = '\0';
}

void removeDuplicateQR(char *data) {
  int len = strlen(data);
  if (len % 2 == 0) {
    int half = len / 2;
    if (strncmp(data, data + half, half) == 0) {
      data[half] = '\0';
    }
  }
}

void clearBuffer(char *buf, uint8_t &index) {
  memset(buf, 0, QR_BUF);
  index = 0;
}

void trimTCPBuffer(char *str) {
  // Remove leading non-printable characters
  while (*str && *str <= 32) memmove(str, str + 1, strlen(str));

  // Remove trailing non-printable characters
  int len = strlen(str);
  while (len > 0 && str[len - 1] <= 32) str[--len] = '\0';
}

// ------------ SETUP ------------
void setup() {
  Serial.begin(9600);
  Serial1.begin(QR_BAUD);   // ENTRY
  Serial2.begin(QR_BAUD);   // EXIT

  pinMode(RELAY, OUTPUT);
  digitalWrite(RELAY, HIGH);

    pinMode(EXIT_RELAY, OUTPUT);
  digitalWrite(EXIT_RELAY, HIGH);

  Ethernet.begin(mac, ip);
  server.begin();

  Serial.print("TCP Server running at IP: ");
  Serial.println(Ethernet.localIP());
}

// ------------ LOOP ------------
void loop() {

  // Check for new client
  if (!client || !client.connected()) {
    EthernetClient newClient = server.available();
    if (newClient) {
      client = newClient;
      Serial.println("Client connected!");
    }
  }

  // ================= ENTRY QR =================
  while (Serial1.available()) {
    char c = Serial1.read();
    Serial.print(c);
    enLastByteTime = millis();
    if (enIndex < QR_BUF - 1) enBuffer[enIndex++] = c;
  }

  if (enIndex > 0 && (millis() - enLastByteTime > qrTimeout)) {
    enBuffer[enIndex] = '\0';
    trimString(enBuffer);
    removeDuplicateQR(enBuffer);

    Serial.print("\nENTRY QR: "); Serial.println(enBuffer);

    if (client && client.connected()) {
      client.print("|ENQR-"); client.print(enBuffer); client.print("%\n");
    }

    clearBuffer(enBuffer, enIndex);
  }

  // ================= EXIT QR =================
  while (Serial2.available()) {
    char c = Serial2.read();
    Serial.print(c);
    exLastByteTime = millis();
    if (exIndex < QR_BUF - 1) exBuffer[exIndex++] = c;
  }

  if (exIndex > 0 && (millis() - exLastByteTime > qrTimeout)) {
    exBuffer[exIndex] = '\0';
    trimString(exBuffer);
    removeDuplicateQR(exBuffer);

    Serial.print("\nEXIT QR: "); Serial.println(exBuffer);

    if (client && client.connected()) {
      client.print("|EXQR-"); client.print(exBuffer); client.print("%\n");
    }

    clearBuffer(exBuffer, exIndex);
  }

  // ================= HEARTBEAT =================
  if (millis() - lastHeartbeat > heartbeatInterval) {
    lastHeartbeat = millis();
    if (client && client.connected()) {
      client.print("|HLT%\n");
      Serial.println("Heartbeat sent");
    }
  }

  // ================= TCP COMMAND READ =================
  while (client && client.connected() && client.available()) {
    char c = client.read();
    Serial.print(c);
    if (tcpIndex < TCP_BUF - 1) tcpBuffer[tcpIndex++] = c;
if (c == '%') {
  tcpBuffer[tcpIndex] = '\0';

  trimTCPBuffer(tcpBuffer);

  Serial.print("\nTCP RECEIVED: "); Serial.println(tcpBuffer);

      if (strncmp(tcpBuffer, "|OPEN", 5) == 0) {
        Serial.println("OPEN ENTRY RELAY");
        client.print("|OK%\n");
        digitalWrite(EXIT_RELAY, LOW);
        delay(1000);
        digitalWrite(EXIT_RELAY, HIGH);
      }
      else if (strncmp(tcpBuffer, "|OPENX", 6) == 0) {
        Serial.println("OPEN EXIT RELAY");
        client.print("|OK%\n");
        digitalWrite(RELAY, LOW);
        delay(1000);
        digitalWrite(RELAY, HIGH);
      }

      memset(tcpBuffer, 0, TCP_BUF);
      tcpIndex = 0;
    }
  }
}
